# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../../castplus_cast_engine_part.gni")
import("../../../../config.gni")

###############################################################################

common_include_dirs = [
  "../",
  "../remote",
  "../../ipc/base",
  "../../ipc/proxy",
  "../../ipc/stub",
  "../../../../interfaces/inner_api/native/session/include",
]

common_deps = [
  "./../../../../frameworks/native/session:avsession_client",
  "./../../../../utils:avsession_utils",
  "./../../../session:avsession_server",
]

common_external_deps = [
  "relational_store:native_rdb",
  "ability_base:configuration",
  "ability_base:want",
  "ability_runtime:wantagent_innerkits",
  "audio_framework:audio_client",
  "cJSON:cjson",
  "c_utils:utils",
  "data_object:distributeddataobject_impl",
  "graphic_surface:surface",
  "hilog:libhilog",
  "input:libmmi-client",
  "ipc:ipc_single",
  "openssl:libcrypto_shared",
  "safwk:system_ability_fwk",
  "samgr:samgr_proxy",
  "window_manager:libdm_lite",
]

common_cflags = []

if (castplus_cast_engine_enable) {
  common_cflags += [ "-DCASTPLUS_CAST_ENGINE_ENABLE" ]
  common_deps += [ "../../../session:avsession_cast_item" ]
}

if (multimedia_av_session_enable_trace_control) {
  common_cflags += [ "-DENBABLE_AVSESSION_TRACE_CONTROL" ]
  common_external_deps += [ "hitrace:hitrace_meter" ]
}

if (multimedia_av_session_enable_sysevent_control) {
  common_cflags += [ "-DENABLE_AVSESSION_SYSEVENT_CONTROL" ]
  common_external_deps += [ "hisysevent:libhisysevent" ]
}

ohos_shared_library("remote_session_source") {
  install_enable = true
  include_dirs = common_include_dirs
  deps = common_deps
  cflags = common_cflags
  external_deps = common_external_deps
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  source_sources = [ "remote_session_source_impl.cpp" ]

  syncers = [
    "remote_session_syncer_impl.cpp",
    "remote_session_capability_set.cpp",
    "json_utils.cpp",
  ]

  sources = source_sources + syncers

  subsystem_name = "multimedia"
  part_name = "av_session"
}

ohos_shared_library("remote_session_sink") {
  install_enable = true
  include_dirs = common_include_dirs
  deps = common_deps
  cflags = common_cflags
  external_deps = common_external_deps
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  sink_sources = [ "remote_session_sink_impl.cpp" ]

  syncers = [
    "remote_session_syncer_impl.cpp",
    "remote_session_capability_set.cpp",
    "json_utils.cpp",
  ]

  sources = sink_sources + syncers

  subsystem_name = "multimedia"
  part_name = "av_session"
}
